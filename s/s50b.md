#Problem 50b Solution 

## Solution
```elm

type Tree a
    = Empty
    | Node a (Tree a) (Tree a)


type alias Freq = (Char, Int)


huffman : List Freq -> List (Char, String)
huffman list =
    huffCodes "" 
        <| huffTree 
        <| List.map (\x -> Node x Empty Empty) list
        

huffCodes : String -> Tree Freq -> List (Char, String)
huffCodes path tree =
    case tree of
        Empty ->
            [('?', "!")]  -- should never get here  
        
        Node (c, f) Empty Empty ->
            [(c, path)]
            
        Node (c, f) left right ->
            (huffCodes (path ++ "0") left) ++ (huffCodes (path ++ "1") right)
           
    

-- build the binary sort tree order by cummulative frequencies
huffTree : List (Tree Freq) -> Tree Freq
huffTree list =
    case List.sortBy treeFreq list of
        [] ->
            Empty
            
        x :: xs ->
            case xs of
                [] ->
                    x
            
                y :: ys ->
                    huffTree ((merge x y) :: ys)
                    
treeFreq : Tree Freq -> Int
treeFreq t = 
    case t of 
        Empty -> 0 
        Node (v, c) l r-> c
        

merge : Tree Freq -> Tree Freq -> Tree Freq
merge t1 t2 = 
    case t1 of
        Empty ->
            Empty -- we should never have empties here 
                    
        Node (xv, xc) xl xr ->
            case t2 of 
                Empty ->
                    Empty
                Node (yv, yc) yl yr ->
                    Node (xv, xc + yc) t1 t2 
```

[Back to problem](../p/p50b.md) 