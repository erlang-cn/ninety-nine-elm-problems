# Problem 23 Solution

```elm
randomSelect : Random.Seed -> Int -> List a -> ( List a, Random.Seed )
randomSelect seed n list =
    let
        ( l, r, s ) =
            randSelect n ( [], list, seed )
    in
        ( l, s )


randSelect : Int -> ( List a, List a, Random.Seed ) -> ( List a, List a, Random.Seed )
randSelect n ( l, r, seed ) =
    if n > 0 then
        let
            ( idx, seed_ ) =
                Random.step (Random.int 1 (List.length r)) seed

            e =
                elementAt r idx

            r_ =
                dropAt r idx
        in
            case e of
                Nothing ->
                    ( l, r, seed )

                Just x ->
                    randSelect (n - 1) ( x :: l, r_, seed_ )
    else
        ( l, r, seed )


{-| return the nth item of a list (one-relative index)
-}
elementAt : List a -> Int -> Maybe a
elementAt list n =
    case list of
        [] ->
            Nothing

        x :: xs ->
            if n == 1 then
                Just x
            else
                elementAt xs (n - 1)


dropAt : List a -> Int -> List a
dropAt list n =
    case list of
        [] ->
            []

        x :: xs ->
            (List.take (n - 1) list) ++ (List.drop n list)
```

[Back to problem](../p/p23.md)

